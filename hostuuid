#!/usr/bin/perl

#This program is free software: you can redistribute it and/or modify
#it under the terms of the GNU General Public License as published by
#the Free Software Foundation, either version 3 of the License, or
#(at your option) any later version.
#
#This program is distributed in the hope that it will be useful,
#but WITHOUT ANY WARRANTY; without even the implied warranty of
#MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#GNU General Public License for more details.
#
#You should have received a copy of the GNU General Public License
#along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Written by Richard Hartmann in 2009, feel free to email
# richih.mailinglist@gmail.com with questions.


use strict;
use warnings;

use UUID::Tiny;
use Getopt::Long qw(:config no_ignore_case bundling);
use File::Basename;

my $version = '1.1';
my $program = basename($0);

my $file = '/etc/hostuuid';
my $print_version;
my $print_help;
my $create_uuid = 0;

GetOptions (
            'file|f=s'  => \$file,
            'help|h|?'  => \$print_help,
            'version|v' => \$print_version,
            'create|c'  => \$create_uuid,
           ) or print_help(255);

sub print_version() {
	print <<VERSION;
$program: version $version

This program is released under GPLv3
Written by Richard Hartmann for Debian and Grml
VERSION
	exit 0;
}

sub print_help {
	my ($exit_code) = @_;
	print <<HELP;
Help for $program:

  -h, -?, --help : print this help
  -v, --version  : print version
  -f, --file     : file to read from/write to. Defaults to /etc/hostuuid
  -c, --create   : create & save UUID
HELP
	exit $exit_code;
}

sub create_uuid() {
	my $uuid = find_hostuuid();
	if ($uuid) {
		print STDERR "$program: Found UUID '$uuid' in '$file'. Aborting!\n";
		exit 101;
	} else {
		open (F, ">", "$file") or die "$program: Could not open file: '$file'\: $!\n";
		$uuid = create_UUID_as_string(UUID_V4);
		print "$program\: Stored UUID '$uuid' in file '$file'\n";
		print F "$uuid\n";
		close (F);
		exit 0;
	}
}

sub find_hostuuid() {
#	if ($create_uuid) {return 0 unless -e $file;}
	return 0 unless ((-e $file && $create_uuid) || !$create_uuid);
	open (F, "<", "$file") or die "$program: Could not open file: '$file'\: $!\n";
	while (<F>) {
		if (/([0-9a-z]{8}-([0-9a-z]{4}-){3}[0-9a-z]{12})/i) {
			close (F);
			return $1;
		}
	}
}

sub main() {
	chomp $file;
	print_version() if $print_version;
	print_help(0)   if $print_help;
	create_uuid()   if $create_uuid;

	my $uuid = find_hostuuid();
	if ($uuid) {
		print "$uuid\n";
		exit 0;
	} else {
		print STDERR "$program: Could not find UUID in '$file'\n";
		exit 100;
	}
}

main();
